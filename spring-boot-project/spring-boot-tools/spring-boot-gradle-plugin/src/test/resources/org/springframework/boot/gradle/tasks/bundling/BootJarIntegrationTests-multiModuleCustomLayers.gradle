plugins {
	id 'java'
	id 'org.springframework.boot' version '{version}'
}

subprojects {
	apply plugin: 'java'
	group = 'org.example.projects'
	version = '1.2.3'
}

bootJar {
	mainClassName = 'com.example.Application'
	layered {
		application {
			intoLayer("static") {
				include "META-INF/resources/**", "resources/**", "static/**", "public/**"
			}
			intoLayer("app")
		}
		dependencies {
			intoLayer("subproject-dependencies") {
				include "org.example.projects:*"
			}
			intoLayer("snapshot-dependencies") {
				include "*:*:*SNAPSHOT"
			}
			intoLayer("commons-dependencies") {
				include "org.apache.commons:*" 
			}
			intoLayer("dependencies")
		}
		layerOrder = ["dependencies", "commons-dependencies", "snapshot-dependencies", "subproject-dependencies", "static", "app"]
	}
}

repositories {
	mavenCentral()
	maven { url "https://repository.apache.org/content/repositories/snapshots" }
}

dependencies {
	implementation(project(':alpha'))
	implementation(project(':bravo'))
	implementation("commons-io:commons-io:2.7-SNAPSHOT")
	implementation("org.apache.commons:commons-lang3:3.9")
	implementation("org.springframework:spring-core:5.2.5.RELEASE")
}

task listLayers(type: JavaExec) {
	classpath = bootJar.outputs.files
	systemProperties = [ "jarmode": "layertools" ]
	args "list"
}

task extractLayers(type: JavaExec) {
	classpath = bootJar.outputs.files
	systemProperties = [ "jarmode": "layertools" ]
	args "extract"
}
